namespace com.ibm.streamsx.kafka.sample ;

use com.ibm.streamsx.kafka::KafkaConsumer ;
use com.ibm.streamsx.kafka::KafkaProducer ;

/*
 * This sample demonstrates how to specify the application configuration
 * name that contains the Kafka properties. Each property in the application 
 * configuration will be loaded as a Kafka property. 
 */
public composite KafkaAppConfigSample
{
    type
        Message = rstring key, rstring message;
    graph
        (stream<Message> KafkaConsumer_1_out0) as KafkaConsumer_1 =
            KafkaConsumer()
        {
            param
                topic : "test" ;
                appConfigName : "kafka-sample";
        }

        () as Custom_2 = Custom(KafkaConsumer_1_out0)
        {
            logic
                onTuple KafkaConsumer_1_out0:
                {
                    println(KafkaConsumer_1_out0);
                }
        }

        () as KafkaProducer_3 = KafkaProducer(Beacon_4_out0)
        {
            param
                topic : "test" ;
                appConfigName : "kafka-sample";
        }

        (stream<Message> Beacon_4_out0) as Beacon_4 = Beacon()
        {
            param
                iterations : 10u;
                initDelay : 5f;
            output Beacon_4_out0:
                key = "key_" + (rstring)IterationCount(),
                message = "msg_" + (rstring)IterationCount();
        }

}

